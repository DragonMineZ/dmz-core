plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "dmzcore_common.mixins.json"
    }
}

architectury {
    common rootProject.enabled_platforms.split(',')
    platformSetupLoomIde()
    forge()
}

configurations {
    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

/**
 * Yes, I know Im including the server in the client.
 * No, this is not a mistake.
 */
dependencies {
    // We depend on Fabric Loader here to use the Fabric @Environment annotations,
    // which get remapped to the correct annotations on each platform.
    // Do NOT use other classes from Fabric Loader.
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    forge "net.minecraftforge:forge:$rootProject.forge_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

